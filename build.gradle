plugins {
    id 'com.gradleup.shadow' version '8.3.5'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.gradleup.shadow'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:26.0.2'
        compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    jar {
        enabled = false
        dependsOn (shadowJar)
    }

    shadowJar {
        archiveClassifier.set('')
        manifest {
            attributes 'Paperweight-Mappings-Namespace': 'mojang'
        }
    }
}

subprojects {
    dependencies {
        implementation project(path: ':', configuration: 'shadow')
    }

    shadowJar {
        archiveBaseName.set(rootProject.name + '-' + project.name)
        destinationDirectory.set(file(rootProject.layout.buildDirectory.dir('libs')))
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(rootProject.properties)
    }
}

repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.6-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.github.timderspieler:DeluxeCombat-API:1.5.0'
    compileOnly 'net.luckperms:api:5.4'

    // Implementation
    compileOnly 'com.saicone:types:1.3.0'
    compileOnly 'com.saicone.rtag:rtag:1.5.11'
    compileOnly 'com.saicone.rtag:rtag-item:1.5.11'
    compileOnly 'com.github.cryptomorin:XSeries:13.3.1'

    // Library loader
    implementation 'com.saicone.ezlib:ezlib:1.3.1'
    implementation 'com.saicone.ezlib:loader:1.3.1'
    compileOnly 'com.saicone.ezlib:annotations:1.3.1'
    annotationProcessor 'com.saicone.ezlib:annotations:1.3.1'
}

jar {
    enabled = false
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')
    relocate 'com.saicone.ezlib', project.group + '.ezlib'
    relocate 'com.saicone.types', project.group + '.libs.types'
    relocate 'com.saicone.rtag', project.group + '.libs.rtag'
    relocate 'com.cryptomorin.xseries', project.group + '.libs.xseries'
}